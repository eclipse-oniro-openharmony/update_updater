# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

FLASHD_HDC_PATH = "//base/update/updater/services/flashd"

common_sources = [
  "${FLASHD_HDC_PATH}/common/async_cmd.cpp",
  "${FLASHD_HDC_PATH}/common/auth.cpp",
  "${FLASHD_HDC_PATH}/common/base.cpp",
  "${FLASHD_HDC_PATH}/common/channel.cpp",
  "${FLASHD_HDC_PATH}/common/debug.cpp",
  "${FLASHD_HDC_PATH}/common/file.cpp",
  "${FLASHD_HDC_PATH}/common/file_descriptor.cpp",
  "${FLASHD_HDC_PATH}/common/forward.cpp",
  "${FLASHD_HDC_PATH}/common/session.cpp",
  "${FLASHD_HDC_PATH}/common/task.cpp",
  "${FLASHD_HDC_PATH}/common/tcp.cpp",
  "${FLASHD_HDC_PATH}/common/transfer.cpp",
  "${FLASHD_HDC_PATH}/common/usb.cpp",
]

config("flashd_hdc_config") {
  include_dirs = [ "${FLASHD_HDC_PATH}/common" ]
  cflags_cc = [ "-std=c++17" ]
}

ohos_source_set("flashd_deamon") {
  sources = [
    "${FLASHD_HDC_PATH}/daemon/daemon.cpp",
    "${FLASHD_HDC_PATH}/daemon/daemon_app.cpp",
    "${FLASHD_HDC_PATH}/daemon/daemon_forward.cpp",
    "${FLASHD_HDC_PATH}/daemon/daemon_tcp.cpp",
    "${FLASHD_HDC_PATH}/daemon/daemon_unity.cpp",
    "${FLASHD_HDC_PATH}/daemon/daemon_usb.cpp",
    "${FLASHD_HDC_PATH}/daemon/jdwp.cpp",
    "${FLASHD_HDC_PATH}/daemon/shell.cpp",
  ]

  sources += common_sources

  defines = [
    "HARMONY_PROJECT",
    "HDC_SUPPORT_FLASHD",
  ]

  configs = [ ":flashd_hdc_config" ]

  deps = [
    "//third_party/libuv:uv_static",
    "//third_party/lz4:liblz4_static",
    "//third_party/openssl:libcrypto_static",
    "//utils/native/base:utils",
  ]

  if (use_musl) {
    deps += [
      "//base/startup/init_lite/interfaces/innerkits/reboot:libreboot_static",
      "//base/startup/syspara_lite/interfaces/innerkits/native/syspara:syspara",
    ]
  }

  include_dirs = [
    "${FLASHD_HDC_PATH}/daemon",
    "//base/update/updater/services/flashd",
    "//base/update/updater/services/flashd/daemon",
    "//base/update/updater/services/flashd/common",
    "//base/update/updater/services/flashd/host",
    "//utils/native/base/include",
    "//third_party/lz4/lib",
    "//third_party/openssl/include",
    "//third_party/libuv",
  ]

  if (use_musl) {
    include_dirs += [
      "//base/startup/syspara_lite/interfaces/innerkits/native/syspara/include",
      "//base/startup/init_lite/interfaces/innerkits/include",
    ]
  }
}

ohos_static_library("libflashd") {
  sources = [
    "//base/update/updater/services/flashd/blockdevice.cpp",
    "//base/update/updater/services/flashd/daemon/daemon_updater.cpp",
    "//base/update/updater/services/flashd/daemon/flashd_main.cpp",
    "//base/update/updater/services/flashd/flash_service.cpp",
    "//base/update/updater/services/flashd/partition.cpp",
    "//base/update/updater/services/updater_binary/update_image_block.cpp",
    "//base/update/updater/services/updater_binary/update_partitions.cpp",
    "//base/update/updater/services/updater_binary/update_processor.cpp",
  ]

  defines = [
    "HDC_SUPPORT_FLASHD",
    "HARMONY_PROJECT",
  ]

  deps = [
    ":flashd_deamon",
    "//base/update/updater/interfaces/kits/misc_info:libmiscinfo",
    "//base/update/updater/services/applypatch:libapplypatch",
    "//base/update/updater/services/diffpatch/patch:libpatch",
    "//base/update/updater/services/fs_manager:libfsmanager",
    "//base/update/updater/services/log:libupdaterlog",
    "//base/update/updater/services/package:libupdaterpackage",
    "//base/update/updater/services/script:libupdaterscript",
    "//base/update/updater/utils:libutils",
    "//third_party/bzip2:libbz2",
    "//third_party/cJSON:cjson_static",
    "//third_party/libuv:uv_static",
    "//third_party/lz4:liblz4_static",
    "//third_party/openssl:libcrypto_static",
    "//third_party/zlib:libz",
    "//utils/native/base:utils",
  ]

  include_dirs = [
    "${FLASHD_HDC_PATH}/daemon",
    "${FLASHD_HDC_PATH}/common",
    "//base/update/updater/services",
    "//base/update/updater/services/flashd",
    "//base/update/updater/services/flashd/daemon",
    "//base/update/updater/interfaces/kits/include",
    "//base/update/updater/services/include/package",
    "//base/update/updater/services/include/script",
    "//base/update/updater/services/include/log",
    "//base/update/updater/services/include",
    "//base/update/updater/utils/include",
    "//third_party/bounds_checking_function/include",
    "//third_party/cJSON",
    "//third_party/openssl/include",
    "//utils/native/base/include",
    "//third_party/lz4/lib",
    "//third_party/libuv",
    "//base/startup/init_lite/interfaces/innerkits/include",
  ]

  part_name = "updater"
}

ohos_executable("ohflash") {
  libs = []
  configs = [ ":flashd_hdc_config" ]
  defines = [
    "HDC_HOST",
    "HARMONY_PROJECT",
  ]
  if (is_mac) {
    defines += [ "HOST_MAC" ]
  }
  cflags_cc = [
    "-Wno-error",
    "-Wno-error=unused-variable",
    "-Wno-unused-variable",
  ]
  sources = [
    "${FLASHD_HDC_PATH}/host/client.cpp",
    "${FLASHD_HDC_PATH}/host/host_app.cpp",
    "${FLASHD_HDC_PATH}/host/host_forward.cpp",
    "${FLASHD_HDC_PATH}/host/host_tcp.cpp",
    "${FLASHD_HDC_PATH}/host/host_unity.cpp",
    "${FLASHD_HDC_PATH}/host/host_usb.cpp",
    "${FLASHD_HDC_PATH}/host/main.cpp",
    "${FLASHD_HDC_PATH}/host/server.cpp",
    "${FLASHD_HDC_PATH}/host/server_for_client.cpp",
    "${FLASHD_HDC_PATH}/host/translate.cpp",
    "${FLASHD_HDC_PATH}/host/host_updater.cpp",
  ]
  sources += common_sources

  deps = [
    "//third_party/libusb:libusb",
    "//third_party/libuv:uv_static",
    "//third_party/lz4:liblz4_static",
    "//third_party/openssl:libcrypto_static",
    "//utils/native/base:utilsecurec",
  ]

  deps += [
      "//third_party/libusb:libusb",
  ]

  include_dirs = [
    "${FLASHD_HDC_PATH}/host",
    "//utils/native/base/include",
    "//third_party/lz4/lib",
    "//third_party/openssl/include",
    "//third_party/libuv",
  ]

  if(is_mingw) {
    static_link = true
    defines += [ "_WIN32" ]
    defines += [ "WIN32_LEAN_AND_MEAN" ]
    libs += [ "setupapi" ]
  }

  if(is_linux) {
    static_link = true
    deps += [ "//third_party/openssl:libcrypto_static" ]
  }

  subsystem_name = "updater"
  part_name = "updater"
}


group("flashhost_target_standard") {
  deps = [
    ":ohflash",
  ]
}

group("flashhost_target_standard_linux") {
  deps = [
    ":ohflash(//build/toolchain/linux:clang_x64)",
  ]
}

group("flashhost_target_standard_mingw") {
  deps = [
    ":ohflash(//build/toolchain/mingw:mingw_x86_64)",
  ]
}

group("flashhost_target_standard_all") {
  deps = [
    ":flashhost_target_standard",
    ":flashhost_target_standard_linux",
    ":flashhost_target_standard_mingw",
  ]
}

group("flashhost_target_standard_windows") {
  deps = [
    ":flashhost_target_standard",
    ":flashhost_target_standard_mingw",
  ]
}

group("updater_flashd") {
  deps = [ ":libflashd" ]
}
