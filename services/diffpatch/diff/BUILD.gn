# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (!is_linux && !is_mingw && !is_mac) {
  import("//base/update/updater/updater_default_cfg.gni")
  updater_path = rebase_path("${updater_absolutely_path}", ".")
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
} else {
  updater_path = "../../.."
}
import("//build/ohos.gni")

config("diff_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${updater_path}/interfaces/kits/include/package",
    "${updater_path}/interfaces/kits/include",
    "${updater_path}/services/common",
    "${updater_path}/services/include/package",
    "${updater_path}/services/include/patch",
    "${updater_path}/services/include",
    "${updater_path}/utils/include",
    "${updater_path}/services/diffpatch/bzip2",
    "${updater_path}/services/diffpatch/diff",
    "${updater_path}/services/diffpatch",
    "${updater_path}/services/package/pkg_manager",
    "//third_party/bounds_checking_function/include",
    "//third_party/bzip2",
  ]
  defines = [ "OPENSSL_SUPPRESS_DEPRECATED" ]
}

ohos_static_library("libdiff") {
  sources = [
    "${updater_path}/services/diffpatch/bzip2/bzip2_adapter.cpp",
    "${updater_path}/services/diffpatch/diff/blocks_diff.cpp",
    "${updater_path}/services/diffpatch/diff/image_diff.cpp",
    "${updater_path}/services/diffpatch/diff/update_diff.cpp",
    "${updater_path}/services/diffpatch/diffpatch.cpp",
  ]

  deps = [
    "${updater_path}/services/log:libupdaterlog",
    "${updater_path}/services/package:libupdaterpackage",
    "//third_party/bounds_checking_function:libsec_static",
    "//third_party/bzip2:libbz2",
    "//third_party/openssl:libcrypto_static",
  ]
  configs = [ ":diff_config" ]

  subsystem_name = "updater"
  part_name = "updater"
}
