# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (!is_linux && !is_mingw && !is_mac) {
  import("//base/update/updater/updater_default_cfg.gni")
  updater_path = rebase_path("${updater_absolutely_path}", ".")
} else {
  updater_path = "../../.."
}
import("//build/ohos.gni")

config("patch_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${updater_path}/interfaces/kits/include/package",
    "${updater_path}/interfaces/kits/include",
    "${updater_path}/services/common",
    "${updater_path}/services/include/log",
    "${updater_path}/services/include/package",
    "${updater_path}/services/include/patch",
    "${updater_path}/services/include",
    "${updater_path}/utils/include",
    "${updater_path}/services/diffpatch/bzip2",
    "${updater_path}/services/diffpatch/patch",
    "${updater_path}/services/diffpatch",
    "${updater_path}/services/package/pkg_manager",
  ]
  defines = [ "OPENSSL_SUPPRESS_DEPRECATED" ]
}

libpatch_common_source = [
  "${updater_path}/services/diffpatch/bzip2/bzip2_adapter.cpp",
  "${updater_path}/services/diffpatch/bzip2/lz4_adapter.cpp",
  "${updater_path}/services/diffpatch/bzip2/zip_adapter.cpp",
  "${updater_path}/services/diffpatch/diffpatch.cpp",
  "${updater_path}/services/diffpatch/patch/blocks_patch.cpp",
  "${updater_path}/services/diffpatch/patch/image_patch.cpp",
  "${updater_path}/services/diffpatch/patch/update_patch.cpp",
]

ohos_static_library("libpatch") {
  sources = libpatch_common_source

  deps = [
    "${updater_path}/services/log:libupdaterlog",
    "${updater_path}/services/package:libupdaterpackage",
  ]

  external_deps = [
    "bounds_checking_function:libsec_static",
    "bzip2:libbz2",
    "lz4:liblz4_static",
    "openssl:libcrypto_static",
    "zlib:libz",
  ]

  configs = [ ":patch_config" ]
  subsystem_name = "updater"
  part_name = "updater"
}

ohos_shared_library("libpatch_shared") {
  sources = libpatch_common_source

  deps = [
    "${updater_path}/interfaces/kits/packages:libpackage_shared",
    "${updater_path}/services/log:libupdaterlog_shared",
    "${updater_path}/services/package:libupdaterpackage_shared",
  ]

  external_deps = [
    "bounds_checking_function:libsec_shared",
    "bzip2:libbz2",
    "lz4:liblz4_static",
    "openssl:libcrypto_shared",
    "zlib:libz",
  ]

  configs = [ ":patch_config" ]
  subsystem_name = "updater"
  part_name = "updater"
}
