# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/update/updater/updater_default_cfg.gni")
import("//build/ohos.gni")

updater_path = rebase_path("${updater_absolutely_path}", ".")

config("libutils_exported_headers") {
  visibility = [ ":*" ]
  include_dirs = [
    "${updater_path}/services/include",
    "${updater_path}/utils/include",
    "${updater_path}/utils/json",
  ]
}

ohos_static_library("libutils") {
  sources = [
    "json/json_node.cpp",
    "partition_utils.cpp",
    "utils.cpp",
    "utils_common.cpp",
    "utils_fs.cpp",
  ]

  include_dirs = [
    "include",
    "${updater_path}/interfaces/kits/include/",
    "${updater_path}/services/common/",
    "${updater_path}/services/include/",
    "//third_party/bounds_checking_function/include",
    "//third_party/cJSON",
  ]

  public_configs = [ ":libutils_exported_headers" ]

  deps = [
    "//third_party/bounds_checking_function:libsec_static",
    "//third_party/cJSON:cjson",
  ]
  external_deps = [ "init:libbegetutil" ]

  if (build_selinux) {
    external_deps += [ "selinux_adapter:librestorecon" ]
    deps += [ "//third_party/selinux:libselinux" ]
    include_dirs += [ "//third_party/selinux/libselinux/include" ]
    cflags = [ "-DWITH_SELINUX" ]
  }

  subsystem_name = "updater"
  part_name = "updater"
}

ohos_static_library("libutils_fs") {
  sources = [ "utils_fs.cpp" ]

  include_dirs = [
    "include",
    "${updater_path}/services/include/",
  ]

  deps = [
    "//third_party/bounds_checking_function:libsec_static",
  ]

  subsystem_name = "updater"
  part_name = "updater"
}

ohos_static_library("libutils_common") {
  sources = [ "utils_common.cpp" ]

  include_dirs = [
    "include",
    "${updater_path}/services/include/",
  ]

  subsystem_name = "updater"
  part_name = "updater"
}

ohos_executable("updater_reboot") {
  sources = [ "updater_reboot.cpp" ]

  include_dirs = [
    "include",
    "${updater_path}/interfaces/kits/include/",
    "${updater_path}/services/include/",
  ]

  deps = [
    "${updater_path}/interfaces/kits/misc_info:libmiscinfo",
    "${updater_path}/services/fs_manager:libfsmanager",
    "${updater_path}/utils:libutils",
  ]
  install_images = [ "updater" ]
  install_enable = true
  part_name = "updater"
  subsystem_name = "updater"
}

ohos_executable("write_updater") {
  sources = [ "write_updater.cpp" ]

  include_dirs = [
    "include",
    "${updater_path}/interfaces/kits/include/",
    "${updater_path}/services/include/",
    "//third_party/bounds_checking_function/include",
    "//base/startup/init/interfaces/innerkits/include",
  ]

  deps = [
    "${updater_path}/interfaces/kits/misc_info:libmiscinfo",
    "//third_party/bounds_checking_function:libsec_static",
  ]
  external_deps = [ "init:libbegetutil" ]

  install_enable = true
  part_name = "updater"
  subsystem_name = "updater"
}
