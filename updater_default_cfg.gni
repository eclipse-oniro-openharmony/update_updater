# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

init_feature_ab_partition = false
declare_args() {
  updater_cfg_file = ""
  updater_ui_support = true
  if (!defined(global_parts_info.arkui_ui_lite)) {
    updater_ui_support = false
  }
  updater_hdc_depend = true
  updater_dcc_support = false
  updater_absolutely_path = "//base/update/updater"
  hdc_base = "//developtools/hdc"
  updater_sign_on_server = false
  updater_zlib_enable = true

  if (defined(global_parts_info) &&
      !defined(global_parts_info.third_party_zlib)) {
    updater_zlib_enable = false
  }
}

if (updater_cfg_file != "") {
  updater_sign_on_server = true
  import("${updater_cfg_file}")
}

template("updater_gen") {
  if (updater_cfg_file != "") {
    ohos_source_set(target_name) {
      forward_variables_from(invoker, "*")
    }
  } else {
    ohos_executable(target_name) {
      if (!is_asan && !is_emulator && target_name == "updater_binary") {
        static_link = true
      }
      forward_variables_from(invoker, "*")

      install_images = [ "updater" ]
      install_enable = true
    }
  }
}
